buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:2.0.0'
    }
}

apply plugin: 'war'

apply plugin: 'netflix.ospackage-tomcat'

apply plugin: "org.akhikhl.gretty"

repositories {
    jcenter()
}

// Gretty allows us to run tomcat from the command line using the "tomcatRun" task.
Properties development_properties = new Properties()
development_properties.load(new FileInputStream(file("src/main/resources/laptop.properties")))
gretty {
    contextPath = '/'
    servletContainer = 'tomcat8'
    systemProperties = development_properties
    scanDirs = ['**/src/main/webapp/**']
    scanDependencies = true
    // More properties can be found here:
    // http://akhikhl.github.io/gretty-doc/Gretty-configuration.html
}

// The ezconfig configuration kicks in when we build our debian package and deploy in AWS.
// It contains all the OS env variable configurations and anything else that tunes our boxes and
// tomcat server.
ospackage {
    packageName = 'vmstransformer'
    requires('nflx-ezconfig')
    requires('nflx-base-www')
}

dependencies {
    // Governator wires up all of our dependencies in dependency injection fashion.
    compile 'com.netflix.governator:governator-core'
    compile 'com.netflix.governator:governator-servlet'
    compile 'com.netflix.governator:governator-jersey'
    // We don't want this to be packaged with the rest. It's only for local runs of our main().
    providedCompile ('com.netflix.governator:governator-jetty') { transitive=false }
    // Same for the servlet libs.
    providedCompile 'org.eclipse.jetty:jetty-servlet'
    providedCompile 'javax.servlet:javax.servlet-api'
    
    compile 'netflix:nf-eventbus-guice'
    compile 'netflix:nf-eventbus'
    compile 'netflix:nf-eventbus-core'

    // Runtime lifecycle modules bring in all the essential Netflix platform dependencies.
    compile 'com.netflix.runtime:runtime-lifecycle'
    compile 'netflix:base-server'

    // Cassandra Aeneas driver
    compile "com.netflix.aeneas:aeneas-core:latest.release"

    // VMS Transformer
    compile project(':vmstransformer-common-interfaces')
    compile project(':vmstransformer-io')
    compile project(':vmstransformer-business-logic')
    compile project(':vmstransformer-publish-workflow')
    compile 'com.netflix.vms.logging:vmslogging-slf4j:latest.release'
    compile 'netflix:vms-hollow-generated-notemplate:56.1076'
    
    compile 'netflix:netflix-internal-hollow:1.+'
    
    // httpclient
    compile 'org.apache.httpcomponents:httpclient'

    testCompile 'junit:junit'
    testCompile 'org.mockito:mockito-all:1.3'
    testCompile 'org.assertj:assertj-core'
    testCompile 'com.netflix.hollow:hollow-diff-ui:2.+'
    testCompile 'com.netflix.hollow:hollow-explorer-ui:2.+'
    testCompile 'com.netflix.hollow:hollow-jsonadapter:2.+'

    // Cassandra Aeneas unit test kit.
    testCompile "com.netflix.aeneas:aeneas-test:latest.release"

    testCompile 'com.netflix.hollow:hollow-diff-ui:2.+'
    testCompile 'org.eclipse.jetty:jetty-server'
}

facets {
    tools {
        parentSourceSet = 'test'
    }
}

// Required exclusions to work around a bug in gretty: log4j-over-slf4j is accidentally included
configurations.grettyRunnerTomcat7 {
    exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
}
configurations.grettyRunnerTomcat8 {
    exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
}

configurations.all {
  resolutionStrategy {
    force 'com.netflix.archaius:archaius2-api:2.2.0'                                                                                                                      
    force 'com.netflix.archaius:archaius2-archaius1-bridge:2.2.0'
    force 'com.netflix.archaius:archaius2-commons-configuration:2.2.0'
    force 'com.netflix.archaius:archaius2-core:2.2.0'
    force 'com.netflix.archaius:archaius2-guice:2.2.0'
    force 'com.netflix.archaius:archaius2-persisted2:2.2.0'
    force 'com.netflix.archaius:archaius2-test:2.2.0'
  }
}


war {
  into('WEB-INF/classes') {
    from 'src/main/java'
  }
}
